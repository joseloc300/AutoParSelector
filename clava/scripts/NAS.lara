import clava.autopar.Parallelize;
import clava.autopar.ParallelizeLoop;
import lara.Io;
import lara.util.LocalFolder;
import clava.ClavaJoinPoints;
import lara.cmake.CMaker;
import weaver.util.WeaverDataStore;
import lara.code.Timer;
import lara.metrics.ExecutionTimeMetric;
import weaver.Query;
import lara.Strings;

import CustomExecutionTimeMetric;
import CustomParallelize;
import Utils;

aspectdef NAS
	input
		sourceFolder,
		benchmarkFolders,
		now,
		readCache
	end

	var benchGroupName = "NAS";
	var start_time = performance.now();
	var nRuns = 3;
	var loopGroupSizeLimit = 2;
	var functionFilters = [];
	var problemSizeFlags = ["-DCLASS_S"/*, "-DCLASS_W", "-DCLASS_A", "-DCLASS_B", "-DCLASS_C", "-DCLASS_D", "-DCLASS_E"*/];

	var NASResults = createBenchGroupResultsObj(nRuns, loopGroupSizeLimit);

	var allFolders = ["BT", "CG", "EP", "FT", "IS", "LU", "MG", "SP", "UA"];
	var foldersToGetExpectedC = ["BT"];
	var excludedFiles = ["expected_output.c"];
	var excludedFolders = ["test"];
	var benchmarks = getSubFolders(sourceFolder, benchmarkFolders, allFolders, excludedFiles, excludedFolders);

	NASResults["totalBenchmarks"] = benchmarks.length;
	NASResults["totalBenchmarkVersions"] = benchmarks.length * problemSizeFlags.length;

	var xtraCs = [];
	var xtraIncludeFolders = [];

	println("Number of benchmarks found: " + benchmarks.length);
	println("Processing benchmarks");
	for (var benchmark of benchmarks) {
		println("Processing: " + benchmark.folder);
		//NEW

		for(var problemSizeFlag of problemSizeFlags) {
			println("Current problem size flag = " + problemSizeFlag);
		
			Clava.pushAst();
			var flags = [problemSizeFlag];
			if(problemSizeFlag == "-DCLASS_W") {
				flags = [];
			}

			var parLoops;
			if(!readCache && foldersToGetExpectedC.includes(benchmark.name)) {
				parLoops = getParLoopsFromPrevOutput(benchmark, xtraCs, xtraIncludeFolders, flags);
			}
			
			rebuildCodeAst(benchmark, xtraCs, xtraIncludeFolders, flags);

			
			var parLoopGroups;
			var benchmarkResults;
			
			if(readCache) {
				parLoops = getObjFromCache(benchmark, benchGroupName, problemSizeFlag, loopGroupSizeLimit, "parLoops");
				if(parLoops["nParLoops"] == 0) {
					Clava.popAst();
					continue;
				}
				//parLoopGroups = getParLoopGroups(parLoops, loopGroupSizeLimit);
				
				parLoopGroups = getObjFromCache(benchmark, benchGroupName, problemSizeFlag, loopGroupSizeLimit, "parLoopGroups");
				benchmarkResults = getObjFromCache(benchmark, benchGroupName, problemSizeFlag, loopGroupSizeLimit, "benchmarkResults");
			}
			else {
				if(!foldersToGetExpectedC.includes(benchmark.name)) {
					Clava.pushAst();
					parLoops = getParLoops(functionFilters);
					Clava.popAst();
				}

				if(Object.keys(parLoops).length == 0) {
					Clava.popAst();
					continue;
				}
				
				benchmarkResults = createBenchResultsObj(benchmark, problemSizeFlag);
				countLoopIterations(benchmark, parLoops, problemSizeFlag);
				parLoopGroups = getParLoopGroups(parLoops, loopGroupSizeLimit);
				addLoopInfo(parLoops, benchmarkResults);
				writeObjToCache(benchmark, benchGroupName, problemSizeFlag, loopGroupSizeLimit, "parLoops", parLoops);
				
				writeObjToCache(benchmark, benchGroupName, problemSizeFlag, loopGroupSizeLimit, "parLoopGroups", parLoopGroups);
				writeObjToCache(benchmark, benchGroupName, problemSizeFlag, loopGroupSizeLimit, "benchmarkResults", benchmarkResults);
				
			}
			
			NASResults["totalParLoops"] += Object.keys(parLoops).length;
			NASResults["totalVersions"] += parLoopGroups.length;
			NASResults["totalRuns"] += parLoopGroups.length * nRuns;
			
			var version = 0;
			for (var loopGroup of parLoopGroups) {
				Clava.pushAst();
	
				addPragmas(loopGroup, parLoops);
	
				var measuresPar = addTimerPar(loopGroup, parLoops);
				var executorsPar = buildAndRun(benchmark, version, loopGroupSizeLimit, nRuns, true, problemSizeFlag);
				addResults(benchmarkResults, measuresPar, executorsPar, version, loopGroup, parLoops);
				version++;
				Clava.popAst();
			}
			
			var parLoopIds = Object.keys(parLoops);
	
			var measuresSeq = addTimerSeq(parLoopIds, parLoops);
			var executorsSeq = buildAndRun(benchmark, version, loopGroupSizeLimit, nRuns, false, problemSizeFlag);
			
			version = -1;
			addResults(benchmarkResults, measuresSeq, executorsSeq, version, loopGroup, parLoops);
			Clava.popAst();
			
			NASResults["benchmarks"].push(benchmarkResults);
			Io.writeJson(benchmark.outputFolder + problemSizeFlag + "/results.json", benchmarkResults);
		}
	}
	var end_time = performance.now();
	NASResults["totalExecutionTimeInSec"] = (end_time - start_time) / 1000;
	
	Io.writeJson("./results/" + benchGroupName + "_" + now.toISOString() + ".json", NASResults);
	println("Finished processing " + benchGroupName);

end
